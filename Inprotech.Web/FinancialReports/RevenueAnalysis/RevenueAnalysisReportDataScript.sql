/* PARAMETER LIST

	@pnFromPeriod	Varchar(254),
	@pnToPeriod	Varchar(254),
	@psDebtorCode Varchar(254),
	@pnUserIdentityId int
*/

SET NOCOUNT ON
SET CONCAT_NULL_YIELDS_NULL OFF

declare @pnFromPeriodID	int
declare @pnToPeriodID	int
declare @bCaseOffice	bit
declare @bHasNameRowAccessSecurity bit
declare @bHasCaseRowAccessSecurity bit

set @bHasNameRowAccessSecurity = 0
set @bHasCaseRowAccessSecurity = 0

select @pnFromPeriodID = PERIODID from PERIOD where LABEL = @pnFromPERIOD
select @pnToPeriodID = PERIODID from PERIOD where LABEL = @pnToPERIOD
Select @bCaseOffice = COLBOOLEAN from SITECONTROL where CONTROLID = 'Row Security Uses Case Office'

Select @bHasNameRowAccessSecurity = 1
		from IDENTITYROWACCESS U WITH (NOLOCK) 
		join ROWACCESSDETAIL R WITH (NOLOCK) on (R.ACCESSNAME = U.ACCESSNAME) 
		where R.RECORDTYPE = 'N'
		and U.IDENTITYID = @pnUserIdentityId

Select @bHasCaseRowAccessSecurity = 1
		from IDENTITYROWACCESS U WITH (NOLOCK) 
		join ROWACCESSDETAIL R WITH (NOLOCK) on (R.ACCESSNAME = U.ACCESSNAME) 
		where R.RECORDTYPE = 'C'
		and U.IDENTITYID = @pnUserIdentityId

SELECT
dbo.fn_FormatName(NENTITY.NAME, NENTITY.FIRSTNAME, NULL, NULL) AS ENTITY,	
dbo.fn_FormatName(N.NAME, N.FIRSTNAME, NULL, NULL) AS DEBTOR,
ISNULL(N.NAMECODE,'000000') AS DEBTORCODE,
CO1.COUNTRY AS DEBTORCOUNTRY,
A.CITY AS DEBTORCITY,
A.STATE AS DEBTORSTATE,
A.POSTCODE AS DEBTORPOSTCODE,
TB.DESCRIPTION	AS DEBTORCATEGORY,
IP.CURRENCY AS DEBTORCURRENCY,
CASE WHEN IP.LOCALCLIENTFLAG = 0 THEN 'FOREIGN' ELSE 'LOCAL' END AS LOCALCLIENTFLAG,
TB1.DESCRIPTION AS DEBTORTYPE,
C.IRN AS CASEIRN,
CT.CASETYPEDESC AS CASETYPE,
PT.PROPERTYNAME AS PROPERTYTYPE,
CO.COUNTRY AS COUNTRYCODE,
F.DESCRIPTION AS CASEOFFICE,
dbo.fn_FormatName(NSTAFF.NAME, NSTAFF.FIRSTNAME, NULL, NULL) AS WIPSTAFF,
PC.PROFITCENTRECODE AS PROFITCENTRECODE,
PC.DESCRIPTION AS PROFITCENTRE, 
WC.DESCRIPTION AS WIPCATEGORY,
WY.DESCRIPTION AS WIPTYPE,
WT.DESCRIPTION AS WIPTEMPLATE,
O.OPENITEMNO AS ITEMNUMBER,
O.ITEMDATE AS ITEMDATE,
O.CURRENCY AS ITEMCURRENCY,
O.LOCALTAXAMT AS ITEMTAXAMT,
WH.LOCALTRANSVALUE * O.BILLPERCENTAGE/100 * -1 AS REVENUE,
--	WH.FOREIGNTRANVALUE * O.BILLPERCENTAGE/100 * -1 AS FOREIGNREVENUE,
CASE WHEN O.CURRENCY IS NULL THEN
	NULL
ELSE
	CASE WHEN WH.FOREIGNCURRENCY = O.CURRENCY THEN
		WH.FOREIGNTRANVALUE * O.BILLPERCENTAGE/100 * -1
	ELSE
		CASE WHEN BL.FOREIGNVALUE IS NOT NULL THEN
			CASE WHEN BL.VALUE <> 0 THEN
				ROUND(WH.LOCALTRANSVALUE * ROUND(BL.FOREIGNVALUE / BL.VALUE, 4),0) * O.BILLPERCENTAGE/100 * -1
			ELSE
				NULL
			END
		ELSE
			NULL
		END
	END
END	AS FOREIGNREVENUE,
WH.POSTPERIOD AS POSTPERIOD,
CASE WHEN WC.CATEGORYCODE = 'PD' AND WH.ASSOCIATENO IS NOT NULL THEN dbo.fn_FormatName(NASSOCIATE.NAME, NASSOCIATE.FIRSTNAME, NULL, NULL) ELSE '' END AS AGENTNAME,
CASE WHEN WC.CATEGORYCODE = 'PD' THEN ISNULL(WH.INVOICENUMBER, '') ELSE '' END AS AGENTINVOICE,
CASE WHEN WC.CATEGORYCODE = 'PD' AND WH.INVOICENUMBER IS NOT NULL THEN WH1.TRANSDATE ELSE NULL END AS AGENTINVOICEDATE,
CASE WHEN WC.CATEGORYCODE = 'PD' AND WH.INVOICENUMBER IS NOT NULL THEN ROUND((WH.LOCALTRANSVALUE - WH.LOCALCOST) / WH.LOCALCOST * 100,0) ELSE NULL END AS AGENTMARGINPERC,
CASE WHEN WC.CATEGORYCODE = 'PD' AND WH.INVOICENUMBER IS NOT NULL THEN WH.LOCALCOST * -1 ELSE NULL END AS AGENTCOSTVALUE

FROM WORKHISTORY WH
LEFT JOIN WORKHISTORY WH1 ON (WH1.ENTITYNO = WH.ENTITYNO
						AND WH1.TRANSNO = WH.TRANSNO
						AND WH1.WIPSEQNO = WH.WIPSEQNO
						AND WH1.HISTORYLINENO = 1
						AND WH1.MOVEMENTCLASS = 1)
JOIN WIPTEMPLATE WT ON (WH.WIPCODE = WT.WIPCODE)
JOIN WIPTYPE WY ON (WT.WIPTYPEID = WY.WIPTYPEID)
JOIN WIPCATEGORY WC ON (WY.CATEGORYCODE = WC.CATEGORYCODE)
JOIN NAME NENTITY ON (WH.REFENTITYNO = NENTITY.NAMENO)
LEFT JOIN dbo.fn_CasesRowSecurity(@pnUserIdentityId) RC1 ON (RC1.CASEID = WH.CASEID)
LEFT JOIN dbo.fn_CasesRowSecurity(@pnUserIdentityId) RC2 ON (RC2.CASEID = WH.CASEID)
LEFT JOIN dbo.fn_CasesEthicalWall(@pnUserIdentityId) C ON ((RC1.CASEID = C.CASEID AND @bCaseOffice = 0) OR (RC2.CASEID = C.CASEID AND @bCaseOffice = 1))
LEFT JOIN CASETYPE CT ON (C.CASETYPE = CT.CASETYPE)
LEFT JOIN PROPERTYTYPE PT ON (C.PROPERTYTYPE = PT.PROPERTYTYPE)
LEFT JOIN COUNTRY CO ON (C.COUNTRYCODE = CO.COUNTRYCODE)
LEFT JOIN OFFICE F ON (F.OFFICEID = C.OFFICEID)
LEFT JOIN NAME NSTAFF ON (WH.EMPLOYEENO = NSTAFF.NAMENO)
JOIN DEBTORHISTORY DH WITH (NOLOCK) ON (DH.REFENTITYNO = WH.REFENTITYNO
			  AND DH.REFTRANSNO = WH.REFTRANSNO
			  AND DH.MOVEMENTCLASS = 1)
JOIN OPENITEM O WITH (NOLOCK) ON (DH.ITEMENTITYNO = O.ITEMENTITYNO
		    AND DH.ITEMTRANSNO = O.ITEMTRANSNO
		    AND DH.ACCTENTITYNO = O.ACCTENTITYNO
		    AND DH.ACCTDEBTORNO = O.ACCTDEBTORNO
		    AND O.ITEMTYPE NOT IN (513,514))
JOIN BILLLINE BL ON (DH.ITEMENTITYNO = BL.ITEMENTITYNO
			AND DH.ITEMTRANSNO = BL.ITEMTRANSNO
			AND WH.BILLLINENO = BL.ITEMLINENO)
JOIN dbo.fn_NamesEthicalWall(@pnUserIdentityId) N ON (DH.ACCTDEBTORNO = N.NAMENO)
JOIN IPNAME IP ON (N.NAMENO = IP.NAMENO)
LEFT JOIN dbo.fn_NamesRowSecurity(@pnUserIdentityId) RN ON (RN.NAMENO = N.NAMENO)
LEFT JOIN ADDRESS A ON (N.POSTALADDRESS = A.ADDRESSCODE)
LEFT JOIN COUNTRY CO1 ON (A.COUNTRYCODE = CO1.COUNTRYCODE)
LEFT JOIN TABLECODES TB ON (IP.CATEGORY = TB.TABLECODE
			   AND TB.TABLETYPE = 6)
LEFT JOIN TABLECODES TB1 ON (IP.DEBTORTYPE = TB1.TABLECODE
			    AND TB1.TABLETYPE = 7)
LEFT JOIN EMPLOYEE E ON (WH.EMPLOYEENO = E.EMPLOYEENO)
LEFT JOIN PROFITCENTRE PC ON (E.PROFITCENTRECODE = PC.PROFITCENTRECODE)
LEFT JOIN NAME NASSOCIATE ON (WH.ASSOCIATENO = NASSOCIATE.NAMENO)
WHERE  WH.STATUS <> 0 
AND WH.POSTPERIOD between @pnFromPeriodID and @pnToPeriodID 
AND WH.MOVEMENTCLASS = 2
AND ISNULL (N.NAMECODE, '000000') LIKE @psDebtorCode
AND (((@bHasNameRowAccessSecurity = 1 AND (RN.READALLOWED = 1 OR RN.INSERTALLOWED = 1 OR RN.UPDATEALLOWED = 1 OR RN.DELETEALLOWED = 1))
		OR (@bHasNameRowAccessSecurity = 0))
AND (
	(@bHasCaseRowAccessSecurity = 1 AND 
	(@bCaseOffice = 0 AND (RC1.READALLOWED = 1 OR RC1.INSERTALLOWED = 1 OR RC1.UPDATEALLOWED = 1 OR RC1.DELETEALLOWED = 1 OR WH.CASEID IS NULL)) OR 
	(@bCaseOffice = 1 AND (RC2.READALLOWED = 1 OR RC2.INSERTALLOWED = 1 OR RC2.UPDATEALLOWED = 1 OR RC2.DELETEALLOWED = 1 OR WH.CASEID IS NULL)))
OR (@bHasCaseRowAccessSecurity = 0)))		
ORDER BY DEBTOR, ENTITY
