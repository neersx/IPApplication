if exists (select * from sysobjects where type='TR' and name = 'InsertActivityAttachment_PushToFirstToFile')
begin
	PRINT 'Refreshing trigger InsertActivityAttachment_PushToFirstToFile...'
	DROP TRIGGER InsertActivityAttachment_PushToFirstToFile
end
go

Create trigger InsertActivityAttachment_PushToFirstToFile on ACTIVITYATTACHMENT for INSERT NOT FOR REPLICATION as
Begin
-- TRIGGER:	InsertActivityAttachment_PushToFirstToFile    
-- VERSION:	3
-- DESCRIPTION:	Place the activity attachment being inserted into First To File import queue.

--				If: the activity attachment is generated with an Activity Request that is 
--					processed by Inprotech Document Generator and
--					it is a delivery method that is based on the 'Disk' delivery type.

--				Or: the activity attachment is generated by Billing and the bill is saved as PDF


-- MODIFICATIONS :
-- Date			Who		Change		Version	Description
-- -----------	-------	------		-------	----------------------------------------------- 
-- 10 Apr 2014	SF		RFC32566 	1		Created
-- 24 Apr 2014	SF		RFC33461	2		Cater for invoices produced from the Billing program
-- 29 Apr 2014	SF		RFC33461	3		Cater for multi-case documents, i.e. those generated with ActivityHistory moved first rather than last.

	If exists(Select * from EXTERNALSETTINGS ES where ES.PROVIDERNAME = 'FirstToFile' and ES.ISCOMPLETE = 1)
	Begin
		---------------------------------------------
		-- If the Firm has configured FTF integration,
		-- EXTERNALSETTINGS.ISCOMPLETE will be 1
		---------------------------------------------
	
		Insert into FTFDOCUMENTUPLOADQUEUE(IDENTITYID, ACTIVITYNO, SEQUENCENO)

		-------------------------------------------------------
		-- Queue the document to be uploaded to First To File if 
		--  - Activity Attachment is inserted by Document Generator 
		--    and the delivery method is based on delivery type of 'Disk'.
		--  - The user who placed the document in the document generator queue has configured FTF credentials.
		-------------------------------------------------------
		
		Select	UI.IDENTITYID, i.ACTIVITYNO, i.SEQUENCENO
		from inserted i
		join ACTIVITY A on (i.ACTIVITYNO = A.ACTIVITYNO)
		
		join ACTIVITYREQUEST AR on (
							A.CASEID = AR.CASEID 
						and A.WHENREQUESTED = AR.WHENREQUESTED 
						and A.SQLUSER = AR.SQLUSER
						and (AR.LOGAPPLICATION = 'Document Generator Server' 
							or AR.LOGAPPLICATION like '%Office%')) -- Batched mode generation, the main processed row in AR.

		join DELIVERYMETHOD DM on (DM.DELIVERYID = AR.DELIVERYID and DM.DELIVERYTYPE = 5302)  -- Disk
						
		left join USERS U on (U.USERID = AR.SQLUSER)
		
		join USERIDENTITY UI on (UI.IDENTITYID = ISNULL(AR.IDENTITYID, U.IDENTITYID))
		
		Union
		
		---------------------------------------------
		-- Queue the document to be uploaded to First To File for documents generated for 
		-- items with same Case Renewal Instructor and Letter 
		-- NOTE: Bulk Renewal Letters moves the Activity Request to Activity History first
		---------------------------------------------
		
		Select	UI.IDENTITYID, i.ACTIVITYNO, i.SEQUENCENO
		from inserted i
		join ACTIVITY A on (i.ACTIVITYNO = A.ACTIVITYNO)
		
		left join ACTIVITYHISTORY AH on (
							A.CASEID = AH.CASEID 
						and A.WHENREQUESTED = AH.WHENREQUESTED 
						and A.SQLUSER = AH.SQLUSER
						and AH.ACTIVITYCODE in (3204, 3206) 
						and AH.LOGAPPLICATION like '%Office%') 
						
		join DELIVERYMETHOD DM on (DM.DELIVERYID = AH.DELIVERYID and DM.DELIVERYTYPE = 5302)  -- Disk
						
		left join USERS U on (U.USERID = AH.SQLUSER)
		
		join USERIDENTITY UI on (UI.IDENTITYID = ISNULL(AH.IDENTITYID, U.IDENTITYID))
		
		Union

		-------------------------------------------------------
		-- Queue the document to be uploaded to First To File if 
		--  - Activity Attachment is inserted by C/S Billing and Web Billing
		--  - The Activity Attachment has been saved to the location specified in 'Bill PDF Directory'
		--	- The Activity was for the Case (excluding debtor at this stage)
		--  - The Firm has configured the essential settings for FTF integration
		--  - The user who placed the document in the document generator queue has configured FTF credentials.
		-------------------------------------------------------
		
		Select	A.LOGIDENTITYID, i.ACTIVITYNO, i.SEQUENCENO
		from inserted i
		join ACTIVITY A on (i.ACTIVITYNO = A.ACTIVITYNO 
						and A.ACTIVITYCATEGORY = 5905
						and A.ACTIVITYTYPE = 5807
						and A.LOGAPPLICATION in ('Billing', 'Inprotech'))
		
		join SITECONTROL PDF on (PDF.CONTROLID = 'Bill Save as PDF' 
							and PDF.COLINTEGER = 2)
		
		join SITECONTROL PDFDIR on (PDFDIR.CONTROLID = 'Bill PDF Directory')
		
		where A.NAMENO is null
		and patindex(PDFDIR.COLCHARACTER + '%', i.[FILENAME]) = 1
		
	End
	
	
End
go
