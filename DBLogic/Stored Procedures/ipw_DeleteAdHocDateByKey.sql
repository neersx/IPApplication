-----------------------------------------------------------------------------------------------------------------------------
-- Creation of ipw_DeleteAdHocDateByKey
-----------------------------------------------------------------------------------------------------------------------------
If exists (select * from dbo.sysobjects where id = object_id(N'[dbo].[ipw_DeleteAdHocDateByKey]') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
Begin
	Print '**** Drop Stored Procedure dbo.ipw_DeleteAdHocDateByKey.'
	Drop procedure [dbo].[ipw_DeleteAdHocDateByKey]
End
Print '**** Creating Stored Procedure dbo.ipw_DeleteAdHocDateByKey...'
Print ''
GO

SET QUOTED_IDENTIFIER OFF
GO
SET ANSI_NULLS ON
GO


CREATE PROCEDURE dbo.ipw_DeleteAdHocDateByKey
(
	@pnUserIdentityId	int,		-- Mandatory
	@psCulture		nvarchar(10) 	= null,
	@pnNameKey		int,		-- Mandatory
	@pdtDateCreated		datetime	
)
as
-- PROCEDURE:	ipw_DeleteAdHocDateByKey
-- VERSION:	4
-- DESCRIPTION:	Delete an Ad Hoc Date with the supplied key without checking for concurrency.

-- MODIFICATIONS :
-- Date		Who	Change	Version	Description
-- -----------	-------	------	-------	----------------------------------------------- 
-- 04 Nov 2004	TM	RF1327	1	Procedure created
-- 14 Dec 2006	PG	RFC4817	2	Delete reminders
-- 21 Apr 2015  DV	R46133  3	Check for NameNo when deleting the employee reminder
-- 16 Dec 2016	MF	70123	4	When deleting EmployeeReminder assocated to ALERT, ensure SOURCE=1.

SET CONCAT_NULL_YIELDS_NULL OFF
-- Row counts required by the data adapter
SET NOCOUNT OFF

Declare	@nErrorCode		int
Declare @sSQLString 		nvarchar(4000)

Declare @nCaseKey		int	
Declare @sAdHocReference	nvarchar(20)
Declare @nSequenceNo 		int
Declare @nNameNo		int

-- Initialise variables
Set @nErrorCode 		= 0



If @nErrorCode = 0 
Begin
	Set @sSQLString = " 
	Select  @nCaseKey = CASEID,
		@sAdHocReference = REFERENCE,
		@nSequenceNo = SEQUENCENO,
		@nNameNo     = NAMENO
 	from ALERT 		
	where   EMPLOYEENO 		= @pnNameKey
	and 	ALERTSEQ		= @pdtDateCreated"
	
	exec @nErrorCode=sp_executesql @sSQLString,
					N'@pnNameKey			int,
					  @pdtDateCreated		datetime,
					  @nCaseKey			int		OUTPUT,
					  @sAdHocReference		nvarchar(20)	OUTPUT,
					  @nSequenceNo			int		OUTPUT,
					  @nNameNo			int		OUTPUT',					  
					  @pnNameKey			= @pnNameKey,
					  @pdtDateCreated		= @pdtDateCreated,
					  @nCaseKey			= @nCaseKey 		OUTPUT,
					  @sAdHocReference		= @sAdHocReference 	OUTPUT,
					  @nSequenceNo			= @nSequenceNo 		OUTPUT,
					  @nNameNo			= @nNameNo		OUTPUT
End
--Delete reminder
If @nErrorCode = 0
Begin
	Set @sSQLString = "
	Delete
	from EMPLOYEEREMINDER
	where EMPLOYEENO 	= @pnNameKey
	and ( CASEID 		= @nCaseKey or  (CASEID is null and @nCaseKey is null))
	and ( REFERENCE 	= @sAdHocReference or (REFERENCE is null and @sAdHocReference is null))
	and ( NAMENO		=  @nNameNo or (NAMENO is null and @nNameNo is null))
	and   SOURCE            = 1 -- indicates the reminder is generated by an Alert
	and   SEQUENCENO 	= @nSequenceNo"  

	exec @nErrorCode=sp_executesql @sSQLString,
					N'@pnNameKey		int,
					  @nCaseKey		int,
					  @sAdHocReference	nvarchar(20),
					  @nSequenceNo		int,
					  @nNameNo		int',
					  @pnNameKey		= @pnNameKey,
					  @nCaseKey		= @nCaseKey,
					  @sAdHocReference	= @sAdHocReference,
					  @nSequenceNo		= @nSequenceNo,
					  @nNameNo		= @nNameNo	
End

If @nErrorCode = 0
Begin
	Set @sSQLString = "	
	Delete
	from 	ALERT
	where   EMPLOYEENO 		= @pnNameKey
	and 	ALERTSEQ		= @pdtDateCreated"

	exec @nErrorCode=sp_executesql @sSQLString,
					N'@pnNameKey			int,
					  @pdtDateCreated		datetime',					  
					  @pnNameKey			= @pnNameKey,
					  @pdtDateCreated		= @pdtDateCreated
End

Return @nErrorCode
GO

Grant execute on dbo.ipw_DeleteAdHocDateByKey to public
GO